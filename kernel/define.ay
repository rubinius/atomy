use(require("core"))

require("atomy/message_structure")

-- function definition
macro(fn(~message): ~*body):
  structure = Atomy MessageStructure new(message)

  Atomy Code DefineFunction new(
    structure name
    `(do: ~*body)
    structure receiver
    structure arguments
    structure default-arguments
    structure splat-argument
    structure post-arguments
    structure proc-argument
  )

macro(fn(~dummy-message)):
  structure = Atomy MessageStructure new(dummy-message)
  Atomy Code DefineFunction new(structure name)


-- method definition
macro(def(~message): ~*body):
  structure = Atomy MessageStructure new(message)

  Atomy Code DefineMethod new(
    structure name
    `(do: ~*body)
    structure receiver
    structure arguments
    structure default-arguments
    structure splat-argument
    structure post-arguments
    structure proc-argument
  )

-- helper for adding a module to the constant scope
with-module = Class new:
  def(bytecode(gen, _)):
    gen push-self
    gen add-scope
    gen push-nil

current-module = Class new:
  def(bytecode(gen, _)):
    gen push-scope
    gen send(.module, 0)

-- module/class opening
macro(~x open: ~*body):
  `(~x module-eval:
      ~(with-module new)
      ~*body)

-- anonymous class creation
macro(class: ~*body): `(Object class: ~*body)

macro(~parent class: ~*body):
  `(//Class new(~parent) open:
      ~*body
      self)

-- named class creation
macro(class(~name): ~*body): `(Object class(~name): ~*body)

macro(~parent class(~name): ~*body):
  `(//Class new(~parent, .~name, ~(current-module new)) open:
      ~*body
      self)

-- singleton class opening
macro(singleton: ~*body): `(self singleton: ~*body)

macro(~x singleton: ~*body):
  `(~x singleton-class open: ~*body)

-- module creation
macro(module: ~*body):
  `(//Module new open:
      ~*body
      self)
